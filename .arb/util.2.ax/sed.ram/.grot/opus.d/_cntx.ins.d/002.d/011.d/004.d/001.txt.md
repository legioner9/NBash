### Переходы в микропрограмме в GNU Sed

В этом разделе мы попробуем прояснить, как применяется микропрограмма к обрабатываемому тексту. Начнем с того, что применение микропрограммы ко входящей строке принято называть циклом. Микропрограмма в цикле применяется к строке с первой по последнюю команду прежде чем перейти к следующей строке и начать новый цикл. Однако, некоторые команды способны прервать цикл, либо перейти в другую его точку. Переходы позволяют реализовать что-то похожее на ветвления, чтобы при определенных условиях выполнять одни команды, а при других условиях — другие.

Следующие команды способны влиять на цикл:

    q
    Безусловно прерывает текущий цикл и завершает обработку всего потока.
    d
    Очищает pattern space и начинает новый цикл.
    D
    Если pattern space пуст, то работает как d, иначе удаляет содержимое pattern space до первой новой строки в нем, перезапускает цикл с результатом, не читая новую строку.
    t <метка>
    Команда, очень похожая на оператор goto в некоторых языках программирования. Делает переход на метку цикла в случае успешного исполнения команды s, чтения очередной строки, либо успешного другого перехода. Если метка отсутствует, то переход выполняется в конец цикла. Данной командой программируются условные переходы.
    T <метка>
    Аналогична команде t, но все условия перехода инвертированы, т.е. команда s выполнилась не успешно, очередная строка не прочиталась.
    b <метка>
    Безусловный переход на указанную метку. Если метка опущена, то переход происходит в конец цикла.

Следующий пример показывает как работает D.

```
 # Примечание: команда l позволяет увидеть содержимое pattern space таким, какое оно есть,
 # вместе со всеми управляющими последовательностями.
 #
$ printf '%s\n' aa bb cc dd | sed -n 'N;l;D'
aa\nbb$
bb\ncc$
cc\ndd$
```

В данном примере цикл состоит из 3 команд. В первом цикле команда `N`, после того как была помещена очередная строка в `pattern space`, добавляет к ней следующую за ней строку. Далее командой l мы выводим содержимое `pattern space`, и вы можете видеть, что в буфере находится конкатенация первых двух строк, разделенная переносом строки. Далее команда D, начиная с начала буфера, удаляет из `pattern space` строку до первого переноса строки, т.е. на момент конца цикла у нас в `pattern space` останется только bb. Затем команда `D` прерывает цикл, и `Sed` переходит на новую строку, но без помещения следующей строки в `pattern space`. Снова команда N добавляет к имеющемуся содержимому следующую строку `(bb\ncc$)` и последовательность действий повторяется.

Вы можете создать метку в любой части цикла. Для этого используется следующий синтаксис: `:имя_метки`. Имя метки должно состоять по меньшей мере из одного символа. Метки не являются командами, поэтому они пропускаются при нормальном течении цикла.

Для уверенного использования данной техники необходима некоторая практика. Следующий пример демонстрирует, как работают переходы в цикле.

```
$ printf '%s\n' a1 a2 a3 | sed '/1/b else ; s/a/z/ ; :else ; y/123/456/'
a4
z5
z6

 # Предыдущая микропрограмма эквивалентна следующей
$ printf '%s\n' a1 a2 a3 | sed -E '/1/!s/a/z/ ; y/123/456/'
a4
z5
z6
```

В этом примере демонстрируется, как строятся конструкции, чем то похожие на if-else, в Sed. Здесь команда `/1/b` else играет роль условия. Если в очередной строке есть цифра 1, то исполнение цикла перейдет на метку :`else`, благодаря команде `b`. За меткой `:else` исполняется команда `y`. Именно из-за перехода мы видим, что в первой строке (где есть цифра 1) изменилась только цифра, но не буква, так как мы перепрыгнули команду, которая изменила бы букву. В оставшихся строках нет цифры 1, поэтому перехода не происходит и на строку применяется две команды, первая из которых изменит букву, а вторая цифру.

Несмотря на всю простоту, даже в Sed можно случайно создавать бесконечные петли. Следующий код демонстрирует бесконечную петлю в Sed, из-за чего исполнение программы никогда не прервется:

```
$ echo "looping" | sed ':begin ; b begin'

 # Когда исполнение микропрограммы доходит до конца, команда b отправляет точку следования в начало и так до бесконечности.
```

Однако, некоторые команды способны самостоятельно разорвать такую петлю из-за особенностей их работы. Например, команды `n` и `N` завершают исполнение Sed, когда им нечего читать в потоке:

```
$ seq 3 | sed ':begin ; n ; bbegin'
1
2
3

$ seq 3 | sed ':begin ; N ; bbegin'
1
2
3

 # В обоих случаях конец потока когда-нибудь настанет, и команда n/N прервет исполнение петли.
```

На практике петли могут использоваться для накапливания информации в буфере pattern space. В официальной документации к GNU Sed приводится пример использования показанной выше петли, в котором она используется для объединения строк. Пусть у нас есть такой текст, использующий QP кодирование.

```
All the wor=
ld's a stag=
e,
And all the=
 men and wo=
men merely =
players:
They have t=
heir exits =
and their e=
ntrances;
And one man=
 in his tim=
e plays man=
y parts.
```

Не вдаваясь в подробности этого кодирования, лишь отметим, что каждая строка завершается символом `=`, который говорит, что продолжение перенесено. Нашей задачей является восстановить оригинальный текст через слияние фрагментов по завершающему символу `=`. В документации приводится такое решение:

```
$ sed ':x ; /=$/ { N ; s/=\n//g ; bx }' text.txt
All the world's a stage,
And all the men and women merely players:
They have their exits and their entrances;
And one man in his time plays many parts.
```

В данном примере петли порождаются только для строк, оканчивающихся знаком =. В таких петлях буфер накапливается строками, содержащими знак равно в конце командой N, а разрыв происходит из-за первых строк, в которых этого символа нет в конце, так как команда перехода в начало будет пропускаться. Печать окончательного результата происходит за счет автоматической печати.

К решению этой задачи есть еще один подход, основанный на условном переходе:

```
$ sed ':x ; $!N ; s/=\n// ; tx ; P ; D' text.txt
All the world's a stage,
And all the men and women merely players:
They have their exits and their entrances;
And one man in his time plays many parts.
```

Это решение похоже на предыдущее, но петля здесь строится из условного перехода `t`: команда `$!N` накапливает данные в буфере, а `s/=\n//` удаляет пару `=\n`; когда команда `s` выполняется успешно, мы переходим в начало программы (т.е. еще есть что накапливать), иначе продолжения нет и мы идем дальше. Командой P мы выводим накопленный результат, а командой D мы начинаем новый цикл, если в потоке есть еще данные. В этом примере команда D также пресекает автоматическую печать.